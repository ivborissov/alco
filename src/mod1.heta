namespace mod1 begin
    t {units: h};

    gut @Compartment {units: L} .= 1; // not used
    blood @Compartment {units: L} .= Vd * body_weight;

    q_a @Species {compartment: gut, isAmount: true, units: g, output: true} .= 0;
    C @Species {compartment: blood, units: g/L, output: true} .= 0;

    BrAC @Record {units: g/L, output: true} := C / 1;

    vabs @Reaction { actors: q_a => C, units: g/h } := k_a * q_a;
    vel @Reaction { actors: C =>, units: g/h } := Vmax * C / (Km + C) * blood;

    k_a @Const {units: 1/h} = 0.062; // 8.33
    Vd @Const {units: L/kg} = 0.457; // 0.58
    Vmax @Const {units: g/L/h} = 0.136; // 0.26
    Km @Const {units: g/L} = 0.096; // 0.092

    adm1 @TimeSwitcher {start: 0};
    adm2 @TimeSwitcher {start: 0.333};
    adm3 @TimeSwitcher {start: 0.667};
    adm4 @TimeSwitcher {start: 1.5};

    q_a [adm1]= q_a + dose_first * body_weight; 
    q_a [adm2]= q_a + dose_first * body_weight;
    q_a [adm3]= q_a + dose_first * body_weight;
    q_a [adm4]= q_a + dose_last * body_weight;

    body_weight @Const {units: kg} = 70;
    dose_first @Const {units: g/kg} = 0.186;
    dose_last @Const  {units: g/kg} = 0.112;

    sigma_add @Const = 0.1; // 6.8e-7
    sigma_prop @Const = 0; // 0.081
    sigma1 @Record {output: true} := sqrt(sigma_add^2 + C^2 * sigma_prop^2);
end
